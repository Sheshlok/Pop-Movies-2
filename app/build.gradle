import com.android.build.OutputFile

/*
 *  Copyright (C) 2016 Sheshlok Samal
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.example.android.popmovies"
        minSdkVersion 14 // Replacing 10 as support library version 26.1.0 has minSDK of 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.0"

        def STRING = "String"
        buildConfigField STRING, "TMDB_API_KEY", "\"${TMDB_API_KEY}\""
    }

    splits {
        // Configures multiple APKs based on ABI
        abi {
            // Enables building multiple APKs per ABI
            enable true

            // By default all ABIs are included, so use reset() and include to specify that
            // we only want APKs for x86, x86_64, armeabi-v7a, arm64-v8a and mips

            // Resets the list of ABIs that Gradle should create APKs for
            reset()

            // Specifies the list of ABIs that Gradle should create APKs for
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"

            // If true, Gradle generates universalAPK containing code and resources for all ABIs in
            // a single APK. Here, it specifies that we don't want to also generate a universal APK
            // that includes all ABIs
            universalApk false
        }
    }

    // map for the version code that gives each ABI a value
    project.ext.abiCodes = [armeabi: 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, mips: 5,
                            mips64: 6, x86: 8, x86_64: 9]

    // For each APK output variant, override versionCode with a combination of ext.abiCodes * 10000 +
    // variant.versionCode. Here, variant.versionCode is equal to defaultConfig.versionCode. If
    // you define product flavors that define their own versionCode, variant.versionCode uses that
    // value instead
    android.applicationVariants.all { variant ->

        // Assigns a different version code for each output APK other than the universal APK
        variant.outputs.each { output ->

            // Stores the value of ext.abiCodes that is associated with the ABI for this variant
            def baseAbiVersionCode =
                    // Determine the ABI for this variant and returns the mapped value
                    project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))

            // Because abiCodes.get() returns null for ABIs that are not mapped by ext.abiCodes ,
            // the following code does not override the version code for universal APKs. However, because
            // we want the universal APKs to have the lowest version code, this outcome is desirable
            if (baseAbiVersionCode != null) {

                // Assign the new version code to versionCodeOverride, which changes the version code
                // for only output APK, not for the variant itself. Skipping this step simply causes
                // Gradle to use the value of variant.versionCode for each APK
                output.versionCodeOverride = baseAbiVersionCode * 1000 + variant.versionCode
            }
        }
    }


    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // Material Design libraries for compatibility
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:palette-v7:26.1.0'

    // Glide: Image Handling library
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    // Retrofit & OkHttp: Networking Library
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'

    // Stetho: A simple debug bridge for Android
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    // ButterKnife: Field and Method binding for Android Views
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Dagger 2: A DI framework to modularize the code
    compile 'com.google.dagger:dagger:2.0.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0.2'

    // RxAndroid: Simplifying API calls, wrapper classes for models, and making app reactive !!!
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.7'

    // Timber: A more useful Android Logging API
    compile 'com.jakewharton.timber:timber:4.6.0'

    // SQLBrite: A light-weight wrapper around SQLiteOpenHelper and ContentResolver which introduces
    // reactive stream semantics to queries
    compile 'com.squareup.sqlbrite:sqlbrite:0.6.2'
//    // LeakCanary: A memory detection library for Android and Java.
//    // "A small leak will sink a great ship" - Benjamin Franklin
//    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
//    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
//    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    // GlidePalette
    // Glide palette for extracting out prominent colors
    compile 'com.github.florent37:glidepalette:1.0.6'

    // Library to observe scroll events on scroll views. Helps in collapsing toolbars in a fragment for e.g.
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'

    // Android Device Shake Detection: Just for fun. Refresh movie list when device is shaken
    compile 'com.squareup:seismic:1.0.2'

    implementation project(':bypass')


}
